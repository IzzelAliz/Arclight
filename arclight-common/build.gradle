buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        jcenter()
        mavenCentral()
        maven { url = 'https://maven.izzel.io/releases' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${forge_gradle_version}"
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath "io.izzel.arclight:arclight-gradle-plugin:$agpVersion"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.izzel.arclight'

ext {
    minecraftVersion = '1.16.5'
    forgeVersion = '36.0.1'
}

arclight {
    mcVersion = minecraftVersion
    forgeVersion = project.ext.forgeVersion
    bukkitVersion = 'v1_16_R3'
    wipeVersion = true
    reobfVersion = false
    accessTransformer = project.file('bukkit.at')
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: "20201028-1.16.3"
    accessTransformer = project.file('src/main/resources/META-INF/accesstransformer.cfg')
}

repositories {
    jcenter()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://files.minecraftforge.net/maven/' }
    maven { url = 'https://jitpack.io/' }
}

dependencies {
    minecraft "net.minecraftforge:forge:$minecraftVersion-$forgeVersion"
    compile "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT@jar"
    compile 'org.jetbrains:annotations:19.0.0'
    compile 'org.spongepowered:mixin:0.8.2'
    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
    compile 'com.github.ArclightTeam:mixin-tools:1.0.0'
    compile 'jline:jline:2.12.1'
    compile 'net.md-5:SpecialSource:1.8.6'
    compile 'org.apache.logging.log4j:log4j-jul:2.11.2'
    compile 'net.md-5:bungeecord-chat:1.16-R0.4'
    compile 'mysql:mysql-connector-java:5.1.49'
    compile 'org.yaml:snakeyaml:1.27'
    compile project(':arclight-api')
    compile project(':i18n-config')
}

remapSpigotJar {
    includes.add('net/minecraft/block/ChestBlock$DoubleInventory')
    includes.add('net/minecraft/tileentity/LecternTileEntity$LecternInventory')
}

generateArclightMeta {
    doFirst {
        throw new StopExecutionException()
    }
}

mixin {
    add sourceSets.main, 'mixins.arclight.refmap.json'
}

compileJava {
    options.compilerArgs << '-XDignore.symbol.file' << '-XDenableSunApiLintControl'
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            "-AreobfTsrgFiles=${project.file('extra_mapping.tsrg').canonicalPath}"
    ]
}
